// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution :  i(15) xx(14-13) a(12) c1c2c3c4c5c6(11-6) d1d2d3(5-3) j1j2j3(2-0)
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).
    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    // Put your code here:
    Mux16(a=instruction, b=pre, sel=instruction[15], out=toAreg);
    Not(in=instruction[15], out=cload);
    //i = false or d1 = trueのとき，Aに書き込む
    Or(a=cload, b=instruction[5], out=Aregload);
    ARegister(in=toAreg, load=Aregload, out=Aregout,out=toPC,out[0..14]=addressM);
    //aにより，Aかメモリのどちらを読み取るか決定
    Mux16(a=Aregout, b=inM, sel=instruction[12], out=toALU);
    //d2 = true and i = trueのとき，Dに書き込む
    And(a=instruction[15], b=instruction[4], out=dregwrite);
    DRegister(in=pre, load=dregwrite, out=Dregout);
    //ALUを記述！！
    ALU(x=Dregout, y=toALU, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM,out=pre, zr=zr, ng=ng);
    //zr,ngとj領域に基づきPCを記述
    Or(a=zr, b=ng, out=nor);
    Not(in=nor, out=pos);
    And(a=instruction[2], b=ng, out=j1);
    And(a=instruction[1], b=zr, out=j2);
    And(a=instruction[0], b=pos, out=j3);
    Or8Way(in[0]=j1,in[1]=j2,in[2]=j3, out=pcload);
    And(a=instruction[15], b=pcload, out=PCload);
    PC(in=toPC, load=PCload, inc=true, reset=reset, out[0..14]=pc);
    //writeM : i = true and d3 = true
    And(a=instruction[15], b=instruction[3], out=writeM);
    
}